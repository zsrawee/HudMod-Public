#############################################################################
##  This file is part of: HudMod Video Editor                              ##
##  https://omar-top.itch.io/hudmod-video-editor                           ##
## ----------------------------------------------------------------------- ##
##  Copyright © 2026 Omar Mohammed Balita.                                 ##
## ----------------------------------------------------------------------- ##
##  This program is free software: you can redistribute it and/or modify   ##
##  it under the terms of the GNU General Public License as published by   ##
##  the Free Software Foundation, either version 3 of the License, or      ##
##  (at your option) any later version.                                    ##
##                                                                         ##
##  This program is distributed in the hope that it will be useful,        ##
##  but WITHOUT ANY WARRANTY; without even the implied warranty of         ##
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the           ##
##  GNU General Public License for more details.                           ##
##                                                                         ##
##  You should have received a copy of the GNU General Public License      ##
##  along with this program. If not, see <https://www.gnu.org/licenses/>.  ##
#############################################################################
class_name TimeLine2 extends EditorControl

signal timeline_view_updated()

enum EditMode {
	MODE_SELECT,
	MODE_SPLIT,
	MODE_SLIP
}

enum EditMultiple {
	EDIT_SINGLE,
	EDIT_MULTIPLE
}

const SMALL_STEP_BY_FPS: Dictionary[int, int] = {
	80: 4,
	40: 2,
	1: 1
}

@onready var header_cont: BoxContainer = IS.create_box_container(12)
@onready var edit_mode_btn: OptionController = IS.create_options_controller_2(0, EditMode)
@onready var edit_multiple_btn: OptionController = IS.create_options_controller_2(1, EditMultiple)
@onready var add_layer_btn: Button = IS.create_button("Add Layer", IS.TEXTURE_ADD)
@onready var split_panelcont: PanelContainer = IS.create_panel_container()
@onready var split_left_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/left-split-clip.png"), null, null, "Split left")
@onready var split_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/split-clip.png"), null, null, "Split")
@onready var split_right_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/right-split-clip.png"), null, null, "Split right")
@onready var marker_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/location-marker.png"), null, null, "New marker")
@onready var comment_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/document.png"), null, null, "New comment")

@onready var clip_path_ctrlr: PathController = PathController.new()
@onready var overlay_menu: Menu = IS.create_menu([
	MenuOption.new("", preload("res://Asset/Icons/AddClipMethods/place_on_top.png")),
	MenuOption.new("", preload("res://Asset/Icons/AddClipMethods/insert.png")),
	MenuOption.new("", preload("res://Asset/Icons/AddClipMethods/overwrite.png")),
	MenuOption.new("", preload("res://Asset/Icons/AddClipMethods/fit_to_fill.png")),
	MenuOption.new("", preload("res://Asset/Icons/AddClipMethods/replace.png"))
], false, false, {custom_minimum_size = Vector2(300., .0)})
@onready var snap_panelcont: PanelContainer = IS.create_panel_container()
@onready var snap_cursor_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/snap (1).png"), null, null, "Snap cursor and timemarkers", true)
@onready var snap_timemarks_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/snap (2).png"), null, null, "Snap time ruler", true)
@onready var snap_clips_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/snap.png"), null, null, "Snap clips", true)
@onready var center_btn: TextureButton = IS.create_texture_button(preload("res://Asset/Icons/world-origin.png"), null, null, "Center")

@onready var body_boxcont: BoxContainer = IS.create_box_container(6, true)
@onready var timemark_panel: TimeMarkPanelContainer = TimeMarkPanelContainer.new(self)
@onready var layers_body: LayersSelectContainer = LayersSelectContainer.new(self)
@onready var scroll_cont: ScrollContainer = IS.create_scroll_container()
@onready var layers_margin_split: SplitContainer = IS.create_split_container(0, true)
@onready var layers_cont: ArrangableBoxContainer = ArrangableBoxContainer.new(layers_body, scroll_cont)
@onready var margin_control: Control = IS.create_empty_control(.0, 100.)
@onready var h_scrollbar: HScrollBar = HScrollBar.new()

@export var navigation_horizontal_speed: float = .1
@export var navigation_vertical_speed: float = 15.
@export var zoom_speed: float = .05
@export var zoom_min: float = .01
@export var zoom_max: float = 10.

@export var edges_h_scale: float = 100.
@export var edges_v_scale: float = 50.
@export var edges_speed_factor_h: float = .1
@export var edges_speed_factor_v: float = 10.

@export var auto_snap: bool
@export var dist_to_snap: float = .1

@export var margin_size: float = 3.

var center: int = 0
var zoom: float = 1.:
	set(val): zoom = clampf(val, zoom_min, zoom_max)

var zoom_factor: float

var domain_len: int
var domain_step: int
var domain_small_step: int

var displ_frame_size: float
var displ_timemark_size_h: float

var edges_nav_horizontal: bool = false:
	set(val): edges_nav_horizontal = val; _update_process_enabling()
var edges_nav_vertical: bool = false:
	set(val): edges_nav_vertical = val; _update_process_enabling()
var edges_nav_velocity: Vector2

var frame_start: int
var frame_end: int

var clips_spacial_frames: PackedInt32Array
var timemarkers_spacial_frames: PackedInt32Array
var spacial_frames: PackedInt32Array

var displ_frame_start: float
var displ_frame_end: float

var predefined_frames: PackedFloat32Array
var small_step_scaler: int

var opened_clip_res: MediaClipRes
var layers: Dictionary[LayerRes, Layer2]

var latest_press_event: InputEventKey



func _ready_editor() -> void:
	
	#region header
	
	var path_scroll_cont: ScrollContainer = IS.create_scroll_container(3, 0)
	path_scroll_cont.add_child(clip_path_ctrlr)
	
	clip_path_ctrlr.set_root_name(&"Root")
	
	overlay_menu.expand_icons = true
	snap_cursor_btn.button_pressed = true
	snap_clips_btn.button_pressed = true
	
	header.add_child(header_cont)
	IS.add_children(header_cont, [
		edit_mode_btn,
		edit_multiple_btn,
		add_layer_btn,
		split_panelcont,
		marker_btn,
		comment_btn,
		path_scroll_cont,
		overlay_menu,
		snap_panelcont,
		center_btn,
	])
	var split_cont: BoxContainer = IS.create_box_container(12)
	var snap_cont: BoxContainer = IS.create_box_container(12)
	split_panelcont.add_child(split_cont)
	snap_panelcont.add_child(snap_cont)
	split_cont.add_child(split_left_btn)
	split_cont.add_child(split_btn)
	split_cont.add_child(split_right_btn)
	snap_cont.add_child(snap_cursor_btn)
	snap_cont.add_child(snap_timemarks_btn)
	snap_cont.add_child(snap_clips_btn)
	
	var overlay_texts: PackedStringArray = ["Place on top", "Insert", "Overwrite", "Fit to fill", "Replace"]
	var overlay_btns: Array[Node] = overlay_menu.buttons_container.get_children()
	for idx: int in overlay_texts.size():
		overlay_btns[idx].tooltip_text = overlay_texts[idx]
	
	IS.expand(path_scroll_cont, true)
	IS.expand(clip_path_ctrlr, true)
	IS.set_button_style(edit_mode_btn, IS.style_button_accent)
	
	edit_mode_btn.selected_option_changed.connect(_on_mode_btn_selected_option_changed)
	edit_multiple_btn.selected_option_changed.connect(_on_edit_multiple_btn_selected_option_changed)
	split_left_btn.pressed.connect(_on_split_left_button_pressed)
	split_btn.pressed.connect(_on_split_button_pressed)
	split_right_btn.pressed.connect(_on_split_right_button_pressed)
	add_layer_btn.pressed.connect(_on_add_layer_btn_pressed)
	marker_btn.pressed.connect(_on_marker_btn_pressed)
	comment_btn.pressed.connect(_on_comment_btn_pressed)

	clip_path_ctrlr.undo_requested.connect(_on_clip_path_ctrlr_undo_requested)
	center_btn.pressed.connect(_on_center_btn_pressed)
	
	#endregion
	
	#region body
	
	body.add_child(body_boxcont)
	IS.add_children(body_boxcont, [timemark_panel, layers_body, h_scrollbar])
	layers_body.add_child(scroll_cont)
	scroll_cont.add_child(layers_margin_split)
	layers_margin_split.add_child(layers_cont)
	layers_margin_split.add_child(margin_control)
	
	clip_contents = true
	
	timemark_panel.custom_minimum_size.y = 30.
	timemark_panel.mouse_filter = Control.MOUSE_FILTER_PASS
	scroll_cont.mouse_filter = Control.MOUSE_FILTER_IGNORE
	layers_body.mouse_filter = Control.MOUSE_FILTER_PASS
	
	layers_body.add_theme_stylebox_override(&"panel", IS.style_box_empty)
	layers_cont.add_theme_constant_override(&"separation", 2)
	
	IS.expand(body_boxcont, true, true)
	IS.expand(layers_body, true, true)
	IS.expand(layers_margin_split, true, true)
	
	body.gui_input.connect(_body_gui_input)
	
	layers_body.resized.connect(update_timeline_view)
	layers_body.selected_changed.connect(update_layers_clips_selection)
	
	scroll_cont.get_v_scroll_bar().scrolling.connect(_on_scroll_cont_scroll_bar_scrolling)
	
	h_scrollbar.scrolling.connect(_on_h_scrollbar_scrolling)
	
	#endregion
	
	#open_project_res(ProjectServer2.project_res)
	#open_clip_res(ProjectServer2.opened_clip_res_path.back())
	ProjectServer2.project_opened.connect(_on_project_server_project_opened)
	ProjectServer2.opened_clip_res_changed.connect(_on_project_server_opened_clip_res_changed)
	PlaybackServer.position_changed.connect(_on_playback_server_position_changed)


func _input(event: InputEvent) -> void:
	if event is InputEventMouseMotion:
		update_edges_navs_velocity()
	elif event is InputEventKey:
		if event.is_pressed(): latest_press_event = event
		else: latest_press_event = null

func _body_gui_input(event: InputEvent) -> void:
	
	if event is InputEventMouseButton:
		
		var method: Callable
		
		if event.ctrl_pressed: method = try_effect_zoom
		elif event.shift_pressed: method = navigate_horizontal
		else: method = navigate_vertical
		
		match event.button_index:
			MOUSE_BUTTON_WHEEL_DOWN:
				method.call(1)
			MOUSE_BUTTON_WHEEL_UP:
				method.call(-1)
		
		update_timeline_view()


func _process(delta: float) -> void:
	apply_edges_navs(delta)

func _update_process_enabling() -> void:
	set_process(edges_nav_horizontal or edges_nav_vertical)

func _draw() -> void:
	
	var timemarkpanel_pos: Vector2 = timemark_panel.global_position - global_position
	var cursor_pos: float = get_display_pos_from_cursor()
	
	if cursor_pos > 268. and cursor_pos <= size.x - 16.: # layer.leftside_panel.size = 250. + body.margin_left = 8. + layer.split_cont.sepration = 12. = 268.
		draw_line(Vector2(cursor_pos, timemarkpanel_pos.y + timemark_panel.size.y + 8.), Vector2(cursor_pos, size.y - (h_scrollbar.size.y + 12. if h_scrollbar.visible else 8.)), IS.color_label, 2.)


func navigate_horizontal(dir: int) -> void:
	var nav_speed: int = dir * (navigation_horizontal_speed * ProjectServer2.project_res.fps) * zoom_factor
	if abs(nav_speed) == 0: center += dir
	else: center += nav_speed
func navigate_horizontal_to(target_center: int, t: float = 1.) -> void: center = lerp(center, target_center, t)
func navigate_to_cursor(nav_dir: int) -> void:
	var cursor_pos: float = get_display_pos_from_cursor()
	if cursor_pos < .0 or cursor_pos > size.x:
		var displacement: int = displ_timemark_size_h / displ_frame_size
		navigate_horizontal_to(PlaybackServer.position + displacement * nav_dir)

func navigate_vertical(dir: int) -> void: scroll_cont.scroll_vertical += dir * navigation_vertical_speed
func navigate_vertical_to(target: int, t: float = 1.) -> void: scroll_cont.scroll_vertical = lerp(scroll_cont.scroll_vertical, target, t)


func try_effect_zoom(dir: int) -> void:
	if EditorServer.graph_editors_focused.is_empty():
		effect_zoom(dir)

func effect_zoom(dir: int) -> void:
	var old_zoom: float = zoom
	var zoom_effect: float = dir * zoom_speed
	
	if zoom < 1.:
		zoom_effect *= maxf(.1, zoom)
	
	zoom += zoom_effect
	
	if zoom == old_zoom:
		return
	
	var displacement: int = (get_frame_from_mouse_pos() - center) * .05
	if zoom > old_zoom: displacement *= -1
	center += displacement


func update_edges_navs_velocity() -> void:
	var mouse_pos: Vector2 = layers_body.get_local_mouse_position()
	var dist_h:= Vector2(mouse_pos.x, layers_body.size.x - mouse_pos.x) # Vector2(dist_left, dist_right)
	var dist_v:= Vector2(mouse_pos.y, layers_body.size.y - mouse_pos.y) # Vector2(dist_up, dist_down)
	
	if dist_h.x < edges_h_scale: edges_nav_velocity.x = dist_h.x - edges_h_scale
	elif dist_h.y < edges_h_scale: edges_nav_velocity.x = edges_h_scale - dist_h.y
	else: edges_nav_velocity.x = .0
	
	if dist_v.x < edges_v_scale: edges_nav_velocity.y = dist_v.x - edges_v_scale
	elif dist_v.y < edges_v_scale: edges_nav_velocity.y = edges_v_scale - dist_v.y
	else: edges_nav_velocity.y = .0

func apply_edges_navs(delta: float) -> void:
	var is_dirty: bool
	
	if edges_nav_horizontal and edges_nav_velocity.x:
		center += edges_nav_velocity.x * zoom_factor * edges_speed_factor_h * ProjectServer2.project_res.fps * delta
		is_dirty = true
	
	if edges_nav_vertical and edges_nav_velocity.y:
		scroll_cont.scroll_vertical += edges_nav_velocity.y * edges_speed_factor_v * delta
		is_dirty = true
	
	if is_dirty:
		update_timeline_view()


func update_timeline_view() -> void:
	
	if ProjectServer2.project_res == null:
		return
	
	await get_tree().process_frame
	
	_update_vars()
	
	_update_horizontal_scrollbar()
	update_layers_clips()
	timemark_panel.update_timemarkpanel_view()
	queue_redraw()
	
	_update_waveforms_pixelate_scale()
	
	timeline_view_updated.emit()


func _update_vars() -> void:
	
	var fps: int = ProjectServer2.project_res.fps
	
	var zoom_base: float
	var zoom_exp: float = ceilf(zoom)
	var zoom_scale_factor: float = zoom_exp - zoom
	
	if zoom >= .5: zoom_base = 2.
	elif zoom >= .25: zoom_base = 1.
	elif zoom >= .125: zoom_base = .5
	else: zoom_base = .25
	
	zoom_factor = pow(zoom_base, zoom_exp)
	
	domain_len = zoom_factor * fps * 10
	domain_step = zoom_factor * fps
	domain_small_step = max(1, zoom_factor * small_step_scaler)
	
	displ_frame_size = timemark_panel.size.x / domain_len * (1. + zoom_scale_factor)
	displ_timemark_size_h = timemark_panel.size.x / 2.


func _update_horizontal_scrollbar() -> void:
	
	var margin_frames: int = margin_size * ProjectServer2.project_res.fps
	
	var farleft: int = get_frame_from_display_pos(.0)
	var farright: int = get_frame_from_display_pos(timemark_panel.size.x)
	var min: int = frame_start - margin_frames - int(250. / displ_frame_size)
	var max: int = frame_end + margin_frames
	var page: int = farright - farleft
	
	h_scrollbar.min_value = min
	h_scrollbar.max_value = max
	h_scrollbar.page = page
	h_scrollbar.value = center - displ_timemark_size_h / displ_frame_size
	
	h_scrollbar.visible = page < max - min


func _update_waveforms_pixelate_scale() -> void:
	var pixel_scale: float
	if zoom > 6.: pixel_scale = 8.
	elif zoom > 4.: pixel_scale = 4.
	elif zoom > 2.: pixel_scale = 2.
	else: pixel_scale = 1.
	MediaServer.WaveformBoxContainer.set_pixelate_scale(pixel_scale)


func switch_edit_mode() -> void:
	edit_mode_btn.selected_id += 1
	if edit_mode_btn.selected_id > edit_mode_btn.options.size() - 1:
		edit_mode_btn.selected_id = 0


func get_display_pos_from_frame(frame: int, control: Control = self) -> float:
	return displ_timemark_size_h + (frame - center) * displ_frame_size + global_position.x - control.global_position.x

func get_display_pos_from_cursor() -> float:
	return get_display_pos_from_frame(PlaybackServer.position)

func get_frame_from_display_pos(pos: float) -> int:
	return round((pos - displ_timemark_size_h) / displ_frame_size + center)

func get_frame_from_mouse_pos() -> int:
	return get_frame_from_display_pos(get_local_mouse_position().x)

func get_snapped_frame_from_display_pos(pos: float) -> int:
	return snap_frame(get_frame_from_display_pos(pos), false, false)

func get_snapped_frame_from_mouse_pos() -> int:
	return snap_frame(get_frame_from_mouse_pos(), false, false)


class TimeMarkPanelContainer extends PanelContainer:
	
	var timeline: TimeLine2
	
	@onready var timemarkers_control: Control = IS.create_empty_control()
	@onready var comments_control: Control = IS.create_empty_control()
	
	var timemarkers: Dictionary[TimeMarkerRes, TimeMarker2]
	var comments: Dictionary[CommentRes, Comment2]
	
	var displayed_frames: Dictionary[int, float] # {frame: display_pos}
	
	var cursor_is_dragging: bool:
		set(val):
			cursor_is_dragging = val
			timeline.edges_nav_horizontal = val
	
	func _init(_timeline: TimeLine2) -> void:
		timeline = _timeline
		clip_contents = true
		IS.set_base_panel_settings(self, IS.style_cornerless_header)
	
	func _ready() -> void:
		add_child(timemarkers_control)
		add_child(comments_control)
		open_project_res(ProjectServer2.project_res)
		ProjectServer2.project_opened.connect(_on_project_server_project_opened)
	
	func _gui_input(event: InputEvent) -> void:
		if Renderer.is_working: return
		if event is InputEventMouseButton:
			if event.button_index == MOUSE_BUTTON_LEFT:
				cursor_is_dragging = event.is_pressed()
				if cursor_is_dragging:
					playback_position_follow_mouse_position()
		elif event is InputEventMouseMotion:
			if cursor_is_dragging:
				playback_position_follow_mouse_position()
	
	func update_timemarkpanel_view() -> void:
		transform_timemarkers()
		transform_comments()
		queue_redraw()
	
	func _draw() -> void:
		
		if ProjectServer2.project_res == null:
			return
		
		var font: Font = IS.label_settings_main.font
		var font_color: Color = IS.label_settings_main.font_color
		
		var size_h: Vector2 = size / 2.
		var size_q: Vector2 = size / 4.
		var str_offeset: float = size_h.y + 8.
		
		var center: int = timeline.center
		var domain_h: int = timeline.domain_len / 2
		var domain_step: int  = timeline.domain_step
		var domain_small_step: int = timeline.domain_small_step
		
		var dist_to_abs: int = snappedi(center, domain_step) - center
		
		displayed_frames.clear()
		
		for frame: int in range(-domain_h - domain_small_step, domain_h + domain_small_step, domain_small_step):
			
			frame += center + dist_to_abs
			
			var displ_pos: float = timeline.get_display_pos_from_frame(frame) - 8.
			var displ_size_y: float
			
			displayed_frames[frame] = displ_pos
			
			if frame % domain_step == 0:
				draw_string(font, Vector2(displ_pos + 10., str_offeset), TimeServer.frame_to_timecode(frame), 0, -1, 16, font_color)
				displ_size_y = size.y
			else:
				displ_size_y = size_q.y
			
			draw_line(Vector2(displ_pos, .0), Vector2(displ_pos, displ_size_y), IS.color_label_transp, 2.)
		
		draw_line(Vector2.ZERO, Vector2(size.x, .0), Color.WEB_GRAY, 2.)
		
		var cursor_pos: float = timeline.get_display_pos_from_cursor()
		
		draw_rect(Rect2(Vector2(cursor_pos - 100., .0), Vector2(200., size.y)), IS.color_label, true)
		var timecode: String = TimeServer.frame_to_timecode(PlaybackServer.position)
		var offset: Vector2 = font.get_string_size(timecode)
		draw_string(font, Vector2(cursor_pos - offset.x / 2., offset.y), timecode, 0, -1, 16, Color(Color.WHITE - IS.color_label_transp, 1.))
		
		displayed_frames.sort()
	
	func playback_position_follow_mouse_position() -> void:
		PlaybackServer.stop()
		PlaybackServer.position = timeline.snap_frame(timeline.get_frame_from_mouse_pos(), true, false)
	
	func open_project_res(project_res: ProjectRes) -> void:
		if not project_res:
			return
		
		for tmr: TimeMarkerRes in timemarkers:
			timemarkers[tmr].queue_free()
		timemarkers.clear()
		
		for cmt: CommentRes in comments:
			comments[cmt].queue_free()
		comments.clear()
		
		var tmrs: Dictionary[int, TimeMarkerRes] = project_res.timemarkers
		for frame: int in tmrs:
			spawn_timemarker(frame, tmrs[frame])
		
		var cmts: Dictionary[int, CommentRes] = project_res.comments
		for frame: int in cmts:
			spawn_comment(frame, cmts[frame])
		
		project_res.timemarker_added.connect(_on_projectres_timemarker_added)
		project_res.timemarker_removed.connect(_on_projectres_timemarker_removed)
		project_res.timemarker_moved.connect(_on_projectres_timemarker_moved)
		
		project_res.comment_added.connect(_on_projectres_comment_added)
		project_res.comment_removed.connect(_on_projectres_comment_removed)
		project_res.comment_moved.connect(_on_projectres_comment_moved)
	
	func spawn_timemarker(frame: int, timemarker_res: TimeMarkerRes) -> void:
		var timemarker:= TimeMarker2.new()
		timemarker.frame = frame
		timemarker.timemarker_res = timemarker_res
		timemarker.custom_minimum_size = Vector2(10., 10.)
		timemarkers_control.add_child(timemarker)
		timemarkers[timemarker_res] = timemarker
		transform_timemarker(frame, timemarker_res)
	
	func free_timemarker(frame: int, timemarker_res: TimeMarkerRes) -> void:
		timemarkers[timemarker_res].queue_free()
		timemarkers.erase(timemarker_res)
	
	func move_timemarker(timemarker_res: TimeMarkerRes, to_frame: int) -> void:
		timemarkers[timemarker_res].frame = to_frame
		transform_timemarker(to_frame, timemarker_res)
	
	func transform_timemarkers() -> void:
		var tmrs: Dictionary[int, TimeMarkerRes] = ProjectServer2.project_res.timemarkers
		for frame: int in tmrs:
			transform_timemarker(frame, tmrs[frame])
	
	func transform_timemarker(frame: int, timemarker_res: TimeMarkerRes) -> void:
		timemarkers[timemarker_res].position.x = timeline.get_display_pos_from_frame(frame) - 8.
	
	func _on_project_server_project_opened(project_res: ProjectRes) -> void:
		open_project_res(project_res)
	
	func _on_projectres_timemarker_added(frame: int, timemarker: TimeMarkerRes) -> void:
		spawn_timemarker(frame, timemarker)
		update_timemarkers_spacial_frames()
	
	func _on_projectres_timemarker_removed(frame: int, timemarker: TimeMarkerRes) -> void:
		free_timemarker(frame, timemarker)
		update_timemarkers_spacial_frames()
	
	func _on_projectres_timemarker_moved(from_frame: int, to_frame: int, timemarker: TimeMarkerRes) -> void:
		move_timemarker(timemarker, to_frame)
		update_timemarkers_spacial_frames()
	
	func update_timemarkers_spacial_frames() -> void:
		timeline.update_timemarkers_spacial_frames()
		timeline.update_spacial_frames()
	
	func spawn_comment(frame: int, comment_res: CommentRes) -> void:
		var comment:= Comment2.new()
		comment.frame = frame
		comment.comment_res = comment_res
		comment.custom_minimum_size = Vector2(10., 10.)
		comments_control.add_child(comment)
		comments[comment_res] = comment
		transform_comment(frame, comment_res)
	
	func free_comment(frame: int, comment_res: CommentRes) -> void:
		comments[comment_res].queue_free()
		comments.erase(comment_res)
	
	func move_comment(comment_res: CommentRes, to_frame: int) -> void:
		comments[comment_res].frame = to_frame
		transform_comment(to_frame, comment_res)
	
	func transform_comments() -> void:
		var cmts: Dictionary[int, CommentRes] = ProjectServer2.project_res.comments
		for frame: int in cmts:
			transform_comment(frame, cmts[frame])
	
	func transform_comment(frame: int, comment_res: CommentRes) -> void:
		comments[comment_res].position.x = timeline.get_display_pos_from_frame(frame) - 8.
	
	func _on_projectres_comment_added(frame: int, comment: CommentRes) -> void:
		spawn_comment(frame, comment)
		update_timemarkers_spacial_frames()
	
	func _on_projectres_comment_removed(frame: int, comment: CommentRes) -> void:
		free_comment(frame, comment)
		update_timemarkers_spacial_frames()
	
	func _on_projectres_comment_moved(from_frame: int, to_frame: int, comment: CommentRes) -> void:
		move_comment(comment, to_frame)
		update_timemarkers_spacial_frames()


class LayersSelectContainer extends SelectContainer:
	
	signal clips_start_move()
	signal clips_end_move()
	
	
	var timeline: TimeLine2
	
	var clips_fordelete: Array[Vector2i]
	
	var min_selected_layer: int
	var max_selected_layer: int
	
	var clips_moving: bool
	var move_start_coords: Vector2i
	var move_layer_delta: int
	var move_frame_delta: int
	var move_insert_dir: int
	
	func _init(_timeline: TimeLine2) -> void:
		timeline = _timeline
	
	func _ready() -> void:
		super()
		shortcut_node.key = &"Timeline"
		shortcut_node.load_shortcuts_from_settings()
		shortcut_node.cond_func = EditorServer.layers_body_shortcut_node_cond_func
	
	func _gui_input(event: InputEvent) -> void:
		super(event)
		if event is InputEventMouseButton:
			if event.button_index == MOUSE_BUTTON_RIGHT:
				if event.is_released():
					popup_options_menu()
	
	func is_moving_clips() -> bool:
		return clips_moving
	
	func start_clips_moving(start_layer_idx: int, start_frame: int) -> void:
		
		var empty_ports: Array[int]
		for port_idx: int in selected:
			var port: Dictionary = selected[port_idx]
			if port.is_empty(): empty_ports.append(port_idx)
		
		for port_idx: int in empty_ports:
			selected.erase(port_idx)
		
		_set_selected_clips_modulate(Color.TRANSPARENT)
		
		clips_moving = true
		move_start_coords = Vector2i(start_layer_idx, start_frame)
		
		timeline.edges_nav_horizontal = true
		timeline.edges_nav_vertical = true
		timeline.update_layers_clips(true)
		timeline.update_clips_spacial_frames(selected_to_vals())
		timeline.update_spacial_frames()
		
		move_clips(start_layer_idx, 0, start_frame)
		clips_start_move.emit()
	
	func move_clips(target_layer_idx: int, insert_dir: int, target_frame: int) -> void:
		
		if target_layer_idx != -1:
			var selectables_keys: Array[int] = selectables.keys()
			move_layer_delta = target_layer_idx - move_start_coords.x
			move_layer_delta = mini(move_layer_delta, selectables_keys.max() - max_selected_layer)
			move_layer_delta = maxi(move_layer_delta, selectables_keys.min() - min_selected_layer)
			if selected.size() == 1:
				move_insert_dir = insert_dir
		
		move_frame_delta = target_frame - move_start_coords.y
		
		var snap_delta: float = INF
		
		for layer_idx: int in selected:
			
			var port: Dictionary = selected[layer_idx]
			
			for frame: int in port:
				
				var clip_res: MediaClipRes = port[frame]
				
				var new_frame: int = frame + move_frame_delta
				var snapped_frame: int = timeline.snap_frame(new_frame, false, false)
				var new_snap_delta: int = snapped_frame - new_frame
				
				if new_snap_delta < snap_delta:
					snap_delta = new_snap_delta
				
				var new_end_frame: int = frame + clip_res.length + move_frame_delta
				var snapped_end_frame: int = timeline.snap_frame(new_end_frame, false, false)
				var new_end_snap_delta: int = snapped_end_frame - new_end_frame
				
				if new_end_snap_delta < snap_delta:
					snap_delta = new_end_snap_delta
		
		if snap_delta != INF:
			move_frame_delta += snap_delta
		
		draw_moved_clips()
	
	func draw_moved_clips() -> void:
		
		var drawable_rect: DrawableRect = EditorServer.drawable_rect
		drawable_rect.clear_drawn_entities()
		
		if move_insert_dir:
			
			var port: Dictionary = selected[move_start_coords.x]
			var layer: Layer2 = timeline.get_layer_from_idx(move_start_coords.x)
			var target_layer: Layer2 = timeline.get_layer_from_idx(move_start_coords.x + move_layer_delta)
			
			for frame: int in port:
				var clip_res: MediaClipRes = port[frame]
				var clip: MediaServer.ClipPanel = layer.get_clip(frame)
				var frame_displ_pos: float = timeline.global_position.x + timeline.get_display_pos_from_frame(frame + move_frame_delta)
				drawable_rect.draw_new_theme_rect(Rect2(frame_displ_pos, target_layer.global_position.y + (.0 if move_insert_dir == 1 else target_layer.size.y) , clip.size.x, 10.))
		
		else:
			
			for layer_idx: int in selected:
				
				var port: Dictionary = selected[layer_idx]
				
				var new_layer_idx: int = layer_idx + move_layer_delta
				
				var layer: Layer2 = timeline.get_layer_from_idx(layer_idx)
				var new_layer: Layer2 = timeline.get_layer_from_idx(new_layer_idx)
				
				var layer_posy: float = new_layer.global_position.y
				
				for frame: int in port:
					var clip: MediaServer.ClipPanel = layer.get_clip(frame)
					var frame_displ_pos: float = timeline.global_position.x + timeline.get_display_pos_from_frame(frame + move_frame_delta)
					var rect: Rect2 = Rect2(Vector2(frame_displ_pos, layer_posy), clip.size)
					drawable_rect.draw_new_theme_rect(rect, IS.color_accent, false)
		
		drawable_rect.queue_redraw()
	
	func end_clips_moving(cancel: bool) -> void:
		
		EditorServer.drawable_rect.clear_drawn_entities()
		_set_selected_clips_modulate(Color.WHITE)
		
		if cancel:
			timeline.update_clips_spacial_frames()
			timeline.update_spacial_frames()
		else:
			move_clips_now()
		
		clips_moving = false
		
		timeline.edges_nav_horizontal = false
		timeline.edges_nav_vertical = false
		
		clips_end_move.emit()
	
	func move_clips_now() -> void:
		
		var from_coords: Array[Vector2i] = selected_to_coords()
		var to_coords: Array[Vector2i] = []
		
		var insert_offset: int = maxi(0, move_insert_dir)
		
		if move_insert_dir:
			var new_layer_idx: int = move_start_coords.x + move_layer_delta + insert_offset
			timeline.opened_clip_res.add_layer(new_layer_idx)
			
			if new_layer_idx <= move_start_coords.x:
				for idx: int in from_coords.size():
					from_coords[idx].x += 1
				insert_offset -= 1
		
		for coord: Vector2i in from_coords:
			to_coords.append(coord + Vector2i(move_layer_delta + insert_offset, move_frame_delta))
		
		timeline.opened_clip_res.move_clips(from_coords, to_coords, timeline.overlay_menu.focus_index)
	
	
	func _get_port_obj(port_idx: int) -> Object:
		return timeline.get_layer_from_idx(port_idx)
	
	func _request_selection_box_select(port_idx: int, port_obj: Object, idx: int) -> bool:
		return selectbox_globalrect.intersects(port_obj.get_clip(idx).get_global_rect())
	
	func _set_focused(new_val: Vector2i) -> void:
		if has_selectable_val(focused.x, focused.y):
			var latest_clip: MediaServer.ClipPanel = timeline.get_layer_from_idx(focused.x).get_clip(focused.y)
			if latest_clip: latest_clip.select_panel.modulate.a = .7
		var new_clip: MediaServer.ClipPanel = timeline.get_layer_from_idx(new_val.x).get_clip(new_val.y)
		if new_clip: new_clip.select_panel.modulate.a = 1.
		super(new_val)
	
	func delete_selected_vals() -> void:
		super()
		timeline.opened_clip_res.remove_clips(clips_fordelete)
		clips_fordelete.clear()
		emit_selected_changed()
	
	func past_selected_vals() -> void:
		super()
		
		if copied.is_empty():
			return
		
		var clips_forpast: Dictionary[Vector2i, MediaClipRes]
		
		var delta: int = PlaybackServer.position - copied_start
		
		for port_idx: int in copied:
			var port: Dictionary = copied[port_idx]
			
			for idx: int in port:
				var frame: int = idx + delta
				var duplicated_clip_res: MediaClipRes = port[idx].duplicate_media_res()
				duplicated_clip_res.move_layers_clips_deep(delta)
				clips_forpast[Vector2i(port_idx, frame)] = duplicated_clip_res
		
		timeline.opened_clip_res.add_clips_by_coords(clips_forpast, timeline.overlay_menu.focus_index)
	
	func _delete_val(port_idx: int, idx: int) -> void:
		clips_fordelete.append(Vector2i(port_idx, idx))
	
	func _past_val(port_idx: int, idx: int) -> void:
		pass
	
	#func group_clips() -> void:
		#pass
	#
	#func ungroup_clips() -> void:
		#pass
	
	func switch_edit_mode() -> void:
		timeline.switch_edit_mode()
	
	func enter_clip() -> void:
		if is_val_selected(focused.x, focused.y):
			ProjectServer2.open_clip_res(get_focused_val())
	
	func exit_clip() -> void:
		ProjectServer2.try_exit_clip_res()
	
	func create_parent() -> void:
		var target_frame: int = PlaybackServer.position
		
		var owner_clip_res: MediaClipRes = timeline.opened_clip_res
		var parent_clip_res:= Display2DClipRes.new()
		parent_clip_res._init_clip_res()
		parent_clip_res.length = 500
		
		var old_children_by_coords: Dictionary[Vector2i, MediaClipRes] = _dictintint_to_dictvec2i(selected, 0, 0, Vector2i.ZERO)
		var new_children_by_coords: Dictionary[Vector2i, MediaClipRes] = _dictintint_to_dictvec2i(selected, 0, target_frame, _get_min_indices(selected))
		var placed_children_by_coords: Dictionary[Vector2i, MediaClipRes] = {}
		var parent_clip_arr: Array[Vector2i] = [Vector2i.ZERO]
		
		var do_method: Callable = func() -> void:
			
			placed_children_by_coords.clear()
			var tmp_placed_ress: Dictionary[Vector2i, MediaClipRes] = parent_clip_res.add_clips_by_coords(new_children_by_coords, 0, true, false)
			for coords: Vector2i in tmp_placed_ress: placed_children_by_coords[coords] = tmp_placed_ress[coords]
			
			owner_clip_res.remove_clips(old_children_by_coords.keys(), true, false)
			
			var place_result: Dictionary[Vector2i, MediaClipRes] = owner_clip_res.add_clips(0, target_frame, [parent_clip_res], timeline.overlay_menu.focus_index, true, false)
			parent_clip_arr[0] = place_result.keys()[0]
		
		var undo_method: Callable = func() -> void:
			owner_clip_res.remove_clips(parent_clip_arr, true, false)
			owner_clip_res.add_clips_by_coords(old_children_by_coords, 0, true, false)
			parent_clip_res.remove_clips(placed_children_by_coords.keys(), true, false)
		
		ProjectServer2.commit_action("create_parent", do_method, undo_method)
	
	func reparent_clip() -> void:
		
		if not is_focused_exists():
			return
		
		var owner_clip_res: MediaClipRes = timeline.opened_clip_res
		var parent_clip_res: MediaClipRes = get_focused_val()
		
		selected[focused.x].erase(focused.y)
		
		var old_children_by_coords: Dictionary[Vector2i, MediaClipRes] = _dictintint_to_dictvec2i(selected, 0, 0, Vector2i.ZERO)
		var new_children_by_coords: Dictionary[Vector2i, MediaClipRes] = _dictintint_to_dictvec2i(selected, 0, PlaybackServer.position, _get_min_indices(selected))
		var placed_children_by_coords: Dictionary[Vector2i, MediaClipRes] = {}
		
		var update_parent_clip_panel: Callable = func() -> void:
			var parent_layer: Layer2 = timeline.get_layer_from_idx(focused.x)
			if not parent_layer.has_clip(focused.y): return
			var parent_clip: MediaServer.ClipPanel = parent_layer.get_clip(focused.y)
			parent_clip.update_has_clips()
			parent_clip.queue_redraw()
		
		var do_method: Callable = func() -> void:
			placed_children_by_coords.clear()
			var tmp_placed_ress: Dictionary[Vector2i, MediaClipRes] = parent_clip_res.add_clips_by_coords(new_children_by_coords, 0, true, false)
			for coords: Vector2i in tmp_placed_ress: placed_children_by_coords[coords] = tmp_placed_ress[coords]
			owner_clip_res.remove_clips(old_children_by_coords.keys(), true, false)
			update_parent_clip_panel.call()
		
		var undo_method: Callable = func() -> void:
			owner_clip_res.add_clips_by_coords(old_children_by_coords, 0, true, false)
			parent_clip_res.remove_clips(placed_children_by_coords.keys(), true, false)
			update_parent_clip_panel.call()
		
		ProjectServer2.commit_action("reparent", do_method, undo_method)
	
	func parent_up(times: int) -> void:
		
		if times == 0: return
		if ProjectServer2.opened_clip_res_path.size() <= times: return
		
		var main_clip_res: MediaClipRes = timeline.opened_clip_res
		var parent_clip_res: MediaClipRes = ProjectServer2.opened_clip_res_path[-times - 1]
		
		var old_children_by_coords: Dictionary[Vector2i, MediaClipRes] = _dictintint_to_dictvec2i(selected, 0, 0, Vector2i.ZERO)
		var placed_children_by_coords: Dictionary[Vector2i, MediaClipRes] = {}
		
		var do_method: Callable = func() -> void:
			placed_children_by_coords.clear()
			var tmp_placed_ress: Dictionary[Vector2i, MediaClipRes] = parent_clip_res.add_clips_by_coords(old_children_by_coords, 0, true, false)
			for coords: Vector2i in tmp_placed_ress: placed_children_by_coords[coords] = tmp_placed_ress[coords]
			main_clip_res.remove_clips(old_children_by_coords.keys(), true, false)
		
		var undo_method: Callable = func() -> void:
			main_clip_res.add_clips_by_coords(old_children_by_coords, 0, true, false)
			parent_clip_res.remove_clips(placed_children_by_coords.keys(), true, false)
		
		ProjectServer2.commit_action("parent_up", do_method, undo_method)
	
	func clear_parents() -> void:
		parent_up(ProjectServer2.opened_clip_res_path.size() - 1)
	
	func open_graph_editors() -> void:
		
		loop_selected_vals({},
			func(port_idx: int, idx: int, info: Dictionary[StringName, Variant]) -> void:
				timeline.get_layer_from_idx(port_idx).get_clip(idx).open_graph_editor()
		)
		update_layers_size()
	
	func close_graph_editors() -> void:
		
		loop_selected_vals({},
			func(port_idx: int, idx: int, info: Dictionary[StringName, Variant]) -> void:
				timeline.get_layer_from_idx(port_idx).get_clip(idx).close_graph_editor()
		)
		update_layers_size()
	
	func split_clips(accept_left: bool, accept_right: bool) -> void:
		timeline.split_clips(accept_left, accept_right)
	
	#func replace_clips() -> void:
		#pass
	#
	#func reverse_clips() -> void:
		#pass
	#
	#func extract_audio() -> void:
		#pass
	
	func save_presets(global: bool) -> void:
		
		var source_clips_ress: Array = selected_to_vals()
		var preset_clips_ress: Array[MediaClipRes]
		
		for clip_res: MediaClipRes in source_clips_ress:
			var preset_tree: Tree = MediaServer.create_clip_res_tree(clip_res)
			var name_edit_cont: EditContainer = IS.create_string_edit("Preset Name", "new preset")
			var name_edit: LineEdit = name_edit_cont.controller
			var box_cont: BoxContainer = WindowManager.popup_accept_window(
				get_window(),
				Vector2i(500, 600),
				"Save %s Preset" % ("Global" if global else "Project"),
					func() -> void:
						var preset_clip_res: MediaClipRes = clip_res.duplicate_media_res()
						preset_clip_res.id = name_edit.text
						preset_clip_res.set_meta(&"preset_offset", clip_res.clip_pos)
						preset_clips_ress.append(preset_clip_res)
			)
			IS.expand(preset_tree, true, true)
			box_cont.add_child(preset_tree)
			box_cont.add_child(name_edit.get_parent())
			name_edit.grab_focus(); name_edit.select_all()
			
			var window: WindowManager.AcceptWindow = box_cont.get_window()
			await window.close_requested
		
		EditorServer.media_explorer.preset_box.create_presets(preset_clips_ress, global)
	
	func _get_clips_options() -> Array[Dictionary]:
		return [
			{as_separator = true},
			{text = "Enter", shortcut = shortcut_node.get_shortcut(&"enter_clip"), metadata = enter_clip},
			{text = "Create Parent", shortcut = shortcut_node.get_shortcut(&"create_parent"), metadata = create_parent},
			{text = "Reparent", shortcut = shortcut_node.get_shortcut(&"reparent"), metadata = reparent_clip},
			{text = "Parent Up", shortcut = shortcut_node.get_shortcut(&"parent_up"), metadata = parent_up.bind(1)},
			{text = "Clear Parents", shortcut = shortcut_node.get_shortcut(&"clear_parents"), metadata = clear_parents},
			{as_separator = true},
			{text = "Open Graph Editor/s", shortcut = shortcut_node.get_shortcut(&"open_graph"), metadata = open_graph_editors},
			{text = "Close Graph Editor/s", shortcut = shortcut_node.get_shortcut(&"close_graph"), metadata = close_graph_editors},
			{as_separator = true},
			{text = "Save as Global Preset", shortcut = shortcut_node.get_shortcut(&"save_global_presets"), metadata = save_presets.bind(true)},
			{text = "Save as Project Preset", shortcut = shortcut_node.get_shortcut(&"save_presets"), metadata = save_presets.bind(false)},
		]
	
	func emit_selected_changed() -> void:
		super()
		
		var selected_layers: Array[int] = selected.keys()
		if selected_layers:
			min_selected_layer = selected_layers.min()
			max_selected_layer = selected_layers.max()
		
		for layer_idx: int in selected:
			
			var port: Dictionary = selected[layer_idx]
			var layer: Layer2 = timeline.get_layer_from_idx(layer_idx)
			
			for frame: int in port:
				layer.get_clip(frame).update_spacial_frames()
		
		timeline.update_clips_spacial_frames()
		timeline.update_spacial_frames()
	
	func update_layers_size() -> void:
		await get_tree().process_frame
		for layer_idx: int in selected:
			timeline.get_layer_from_idx(layer_idx).update_size()
	
	
	func _set_selected_clips_modulate(to: Color) -> void:
		for layer_idx: int in selected:
			var port: Dictionary = selected[layer_idx]
			var layer: Layer2 = timeline.get_layer_from_idx(layer_idx)
			for frame: int in port:
				layer.get_clip(frame).modulate = to
	
	func _get_min_indices(dict: Dictionary[int, Dictionary]) -> Vector2i:
		
		const INT_MAX: int = (1 << 63) - 1
		var min_port_idx: int = INT_MAX
		var min_idx: int = INT_MAX
		
		for port_idx: int in dict:
			var port: Dictionary = dict[port_idx]
			min_port_idx = min(min_port_idx, port_idx)
			for idx: int in port:
				min_idx = min(min_idx, idx)
		
		return Vector2i(min_port_idx, min_idx)
	
	func _dictintint_to_dictvec2i(dict: Dictionary[int, Dictionary], layer_start: int, frame_start: int, min_indices: Vector2i) -> Dictionary[Vector2i, MediaClipRes]:
		var new_dict: Dictionary[Vector2i, MediaClipRes]
		
		var min_layer: int = min_indices.x
		var min_frame: int = min_indices.y
		
		for port_idx: int in dict:
			var port: Dictionary = dict[port_idx]
			var target_layer: int = port_idx - min_layer + layer_start
			for idx: int in port:
				var target_frame: int = idx - min_frame + frame_start
				new_dict[Vector2i(target_layer, target_frame)] = port[idx].duplicate_media_res()
		
		return new_dict


func update_clips_spacial_frames(ignored_clips: Array = []) -> void:
	clips_spacial_frames.clear()
	
	frame_start = opened_clip_res.clip_pos
	frame_end = frame_start + opened_clip_res.length
	
	displ_frame_start = get_display_pos_from_frame(frame_start)
	displ_frame_end = get_display_pos_from_frame(frame_end)
	
	for layer_res: LayerRes in opened_clip_res.layers:
		var clips: Dictionary[int, MediaClipRes] = layer_res.clips
		for frame: int in clips:
			var clip_res: MediaClipRes = clips[frame]
			if ignored_clips.has(clip_res):
				continue
			clips_spacial_frames.append(frame)
			clips_spacial_frames.append(frame + clip_res.length)
	
	var selected_clips: Dictionary[int, Dictionary] = layers_body.selected
	
	for layer_idx: int in selected_clips:
		var port: Dictionary = selected_clips[layer_idx]
		var layer: Layer2 = get_layer_from_idx(layer_idx)
		for frame: int in port:
			if ignored_clips.has(port[frame]):
				continue
			var clip_panel = layer.get_clip(frame)
			if clip_panel == null or clip_panel.select_panel == null:
				continue
			var clip_spacial_frames: PackedInt32Array = clip_panel.select_panel.get_spacial_frames()
			for spacial_frame: int in clip_spacial_frames:
				clips_spacial_frames.append(frame + spacial_frame)
	
	clips_spacial_frames.append(frame_start)
	clips_spacial_frames.append(frame_end)
	
	clips_spacial_frames.sort()

func update_timemarkers_spacial_frames() -> void:
	timemarkers_spacial_frames.clear()
	for frame: int in ProjectServer2.project_res.timemarkers:
		timemarkers_spacial_frames.append(frame)
	for frame: int in ProjectServer2.project_res.comments:
		timemarkers_spacial_frames.append(frame)
	timemarkers_spacial_frames.sort()

func update_spacial_frames() -> void:
	spacial_frames = clips_spacial_frames + timemarkers_spacial_frames
	spacial_frames.sort()

func update_all_spacial_frames() -> void:
	update_clips_spacial_frames()
	update_timemarkers_spacial_frames()
	update_spacial_frames()


func snap_frame(frame: int, ignore_cursor: bool, ignore_timemarkers: bool, ignore_frames: PackedInt32Array = []) -> int:
	
	if not auto_snap and (not latest_press_event or not latest_press_event.keycode == KEY_CTRL):
		return frame
	
	var _dist_to_snap: float = (dist_to_snap * ProjectServer2.fps) * zoom_factor
	var dist: float = INF
	
	if snap_timemarks_btn.button_pressed:
		frame = snap_with_timemarks(frame)
	
	if snap_clips_btn.button_pressed:
		var snap_frame: int = snap_with_clips(frame)
		var new_dist: int = absi(snap_frame - frame)
		if new_dist < _dist_to_snap and new_dist < dist:
			dist = new_dist
			frame = snap_frame
	
	if snap_cursor_btn.button_pressed:
		var snap_frame: int = snap_with_cursor_and_timemarkers(frame, ignore_cursor, ignore_timemarkers)
		var new_dist: int = absi(snap_frame - frame)
		if new_dist < _dist_to_snap and new_dist < dist:
			dist = new_dist
			frame = snap_frame
	
	return frame

func snap_with_timemarks(frame: int) -> int:
	return snappedi(frame, domain_small_step)

func snap_with_clips(frame: int) -> int:
	if clips_spacial_frames.is_empty():
		return frame
	return clips_spacial_frames[ArrHelper.int32_array_find_closest(frame, clips_spacial_frames)]

func snap_with_cursor_and_timemarkers(frame: int, ignore_cursor: bool, ignore_timemarkers: bool) -> int:
	var target_frame: int = frame
	var target_dist: float = INF
	
	if not ignore_timemarkers:
		if not timemarkers_spacial_frames.is_empty():
			var timemarker_target_idx: int = ArrHelper.int32_array_find_closest(frame, timemarkers_spacial_frames)
			target_frame = timemarkers_spacial_frames[timemarker_target_idx]
			target_dist = absi(target_frame - frame)
	
	if not ignore_cursor:
		var dist_to_cursor: int = absi(PlaybackServer.position - frame)
		if dist_to_cursor < target_dist: return PlaybackServer.position
	
	return target_frame

func get_next_spacial_frame(frame: int, step: int) -> int:
	
	if spacial_frames.is_empty():
		return frame
	
	var size: int = spacial_frames.size()
	var curr_idx: int = ArrHelper.int32_array_find_leftright(frame, spacial_frames).y
	
	if curr_idx < 0:
		curr_idx = 0
	elif curr_idx >= size:
		curr_idx = size - 1
	
	if spacial_frames[curr_idx] == frame:
		var target_idx: int = curr_idx + step
		if target_idx >= size:
			return spacial_frames[0]
		elif target_idx < 0:
			return spacial_frames[size - 1]
		return spacial_frames[target_idx]
	
	var target_idx: int = curr_idx + step
	if target_idx >= size:
		return spacial_frames[0]
	elif target_idx < 0:
		return spacial_frames[size - 1]
	return spacial_frames[target_idx]

func open_project_res(project_res: ProjectRes) -> void:
	
	update_predefined_frames(project_res.fps)
	
	var min_small_step: int
	small_step_scaler = 0
	
	for fps_channel: int in SMALL_STEP_BY_FPS:
		if project_res.fps >= fps_channel:
			min_small_step = SMALL_STEP_BY_FPS[fps_channel]
			break
	
	for frame: int in predefined_frames:
		if frame > min_small_step:
			small_step_scaler = frame
			break

func update_predefined_frames(fps: int) -> void:
	
	predefined_frames = [.25, .5, 1.]
	
	var divs: PackedInt32Array = CustomMath.get_divisors(fps)
	for div: int in divs:
		predefined_frames.append(div)
	
	predefined_frames.append(fps)
	
	const TIME_MULTIPLIER: PackedInt32Array = [2, 5, 10, 30, 60, 120, 300, 600, 1800, 3600, 7200]
	
	for m: int in TIME_MULTIPLIER:
		var step_val: int = fps * m
		if not predefined_frames.has(step_val):
			predefined_frames.append(step_val)
	
	predefined_frames.sort()


func open_clip_res(clip_res: MediaClipRes) -> void:
	
	if opened_clip_res: _disconnect_clip_res(opened_clip_res)
	if not clip_res: return
	
	_connect_clip_res(clip_res)
	opened_clip_res = clip_res
	
	for layer_res: LayerRes in layers:
		layers[layer_res].queue_free()
	layers.clear()
	
	var layers_ress: Array[LayerRes] = clip_res.layers
	for layer_idx: int in layers_ress.size():
		var layer_res: LayerRes = layers_ress[layer_idx]
		var layer_clips: Dictionary[int, MediaClipRes] = layer_res.get_clips()
		var layer: Layer2 = spawn_layer(layer_idx, layer_res)
		for frame: int in layer_clips:
			layer.spawn_clip(frame, layer_clips[frame], false)
	
	var clip_res_path: Array[MediaClipRes] = ProjectServer2.opened_clip_res_path
	var string_path: Array
	for idx: int in range(1, clip_res_path.size()):
		var path_clip_res: MediaClipRes = clip_res_path[idx]
		string_path.append(path_clip_res.get_display_name())
	clip_path_ctrlr.update(string_path)
	
	await sort_layers()
	update_layers_clips(true)
	update_timemarkers_spacial_frames()
	update_when_clips_changed()
	_update_process_enabling()

func _disconnect_clip_res(clip_res: MediaClipRes) -> void:
	clip_res.layer_added.disconnect(_on_clip_res_layer_added)
	clip_res.layer_removed.disconnect(_on_clip_res_layer_removed)
	clip_res.layer_moved.disconnect(_on_clip_res_layer_moved)
	clip_res.clips_added.disconnect(_on_clip_res_clips_added)
	clip_res.clips_removed.disconnect(_on_clip_res_clips_removed)
	clip_res.clips_moved.disconnect(_on_clip_res_clips_moved)
	clip_res.clips_splited.disconnect(_on_clip_res_clips_splited)
	clip_res.clips_updated.disconnect(_on_clip_res_clips_updated)

func _connect_clip_res(clip_res: MediaClipRes) -> void:
	clip_res.layer_added.connect(_on_clip_res_layer_added)
	clip_res.layer_removed.connect(_on_clip_res_layer_removed)
	clip_res.layer_moved.connect(_on_clip_res_layer_moved)
	clip_res.clips_added.connect(_on_clip_res_clips_added)
	clip_res.clips_removed.connect(_on_clip_res_clips_removed)
	clip_res.clips_moved.connect(_on_clip_res_clips_moved)
	clip_res.clips_splited.connect(_on_clip_res_clips_splited)
	clip_res.clips_updated.connect(_on_clip_res_clips_updated)


func get_layer_from_idx(layer_idx: int) -> Layer2:
	return get_layer(opened_clip_res.get_layer(layer_idx))

func get_layer(layer_res: LayerRes) -> Layer2:
	return layers[layer_res]

func spawn_layer(layer_idx: int, layer_res: LayerRes) -> Layer2:
	var layer: Layer2 = Layer2.new()
	layer.layer_res = layer_res
	layer.layer_idx = layer_idx
	layers_cont.add_child(layer)
	layers[layer_res] = layer
	return layer

func free_layer(layer_res: LayerRes) -> void:
	layers[layer_res].queue_free()
	layers.erase(layer_res)
	sort_layers()

func is_layer_hidden(layer: Layer2) -> bool:
	return not get_global_rect().intersects(layer.get_global_rect())

func find_layer_that_contains_mouse() -> Layer2:
	var mouse_glob_pos: Vector2 = get_global_mouse_position()
	
	for layer_res: LayerRes in layers:
		var layer: Layer2 = layers[layer_res]
		if layer.get_global_rect().has_point(mouse_glob_pos):
			return layer
	
	return null

func sort_layers() -> void:
	
	layers_body.clear_selectable_ports()
	layers_body.emit_selected_changed()
	
	var layers_ress: Array[LayerRes] = opened_clip_res.layers
	var layers_size: int = layers_ress.size()
	
	for layer_idx: int in layers_size:
		var layer_res: LayerRes = layers_ress[layer_idx]
		
		var layer: Layer2 = layers[layer_res]
		layer.layer_idx = layer_idx
		
		layers_cont.move_child(layer, layers_size - layer_idx - 1)
		layer.update_clips_coords()
		layer.update_customization()
		
		if not layer_res.locked:
			layers_body.add_selectable_port(layer_idx, layer_res.get_clips().duplicate())


func update_layers_clips(force_update: bool = false) -> void:
	var layer_skip_cond: Callable
	if force_update: layer_skip_cond = func(layer: Layer2) -> bool: return false
	else: layer_skip_cond = func(layer: Layer2) -> bool: return is_layer_hidden(layer)
	
	var layers_ress: Array[LayerRes] = opened_clip_res.layers
	
	for idx: int in layers_ress.size():
		var layer: Layer2 = get_layer_from_idx(idx)
		if layer_skip_cond.call(layer):
			continue
		layer.update_clips_transform()

func update_layers_clips_selection() -> void:
	var layers_ress: Array[LayerRes] = opened_clip_res.layers
	var selected: Dictionary[int, Dictionary] = layers_body.selected
	for idx: int in layers_ress.size():
		get_layer_from_idx(idx).update_clips_selection(selected[idx] if selected.has(idx) else {})

func update_layers_customization() -> void:
	var layers_ress: Array[LayerRes] = opened_clip_res.layers
	for idx: int in layers_ress.size():
		get_layer_from_idx(idx).update_customization()

func update_when_clips_changed() -> void:
	await get_tree().process_frame
	update_layers_clips()
	update_layers_clips_selection()
	update_clips_spacial_frames()
	update_spacial_frames()
	_update_horizontal_scrollbar()
	await get_tree().process_frame
	PlaybackServer.seek_here()



func spawn_clips(clips: Dictionary[Vector2i, MediaClipRes]) -> void:
	for coord: Vector2i in clips:
		var clip_res: MediaClipRes = clips[coord]
		var layer: Layer2 = get_layer_from_idx(coord.x)
		layer.spawn_clip(coord.y, clip_res, false)
		layers_body.add_selectable_val(coord.x, coord.y, clip_res)

func free_clips(clips_coords: Array[Vector2i]) -> void:
	for coord: Vector2i in clips_coords:
		var layer: Layer2 = get_layer_from_idx(coord.x)
		if layer.has_clip(coord.y):
			layer.free_clip(coord.y)
			layers_body.delete_selectable_val(coord.x, coord.y)
			if layers_body.selected.has(coord.x):
				layers_body.selected[coord.x].erase(coord.y)

func update_clips(clips_coords: Array[Vector2i]) -> void:
	for coord: Vector2i in clips_coords:
		get_layer_from_idx(coord.x).update_clip_ui(coord.y)


func split_clips(accept_left: bool, accept_right: bool) -> void:
	opened_clip_res.split_clips(layers_body.selected_to_coords(), PlaybackServer.position, accept_left, accept_right)


func _on_mode_btn_selected_option_changed(id: int, option: MenuOption) -> void:
	pass

func _on_edit_multiple_btn_selected_option_changed(id: int, option: MenuOption) -> void:
	pass


func _on_split_left_button_pressed() -> void:
	split_clips(true, false)

func _on_split_button_pressed() -> void:
	split_clips(true, true)

func _on_split_right_button_pressed() -> void:
	split_clips(false, true)

func _on_add_layer_btn_pressed() -> void:
	await get_tree().process_frame
	opened_clip_res.add_layer(opened_clip_res.layers.size())

func _on_marker_btn_pressed() -> void:
	ProjectServer2.commit_action(
		"add_marker",
		ProjectServer2.project_res.add_timemarker.bind(PlaybackServer.position),
		ProjectServer2.project_res.remove_timemarker.bind(PlaybackServer.position)
	)

func _on_comment_btn_pressed() -> void:
	ProjectServer2.commit_action(
		"add_comment",
		ProjectServer2.project_res.add_comment.bind(PlaybackServer.position),
		ProjectServer2.project_res.remove_comment.bind(PlaybackServer.position)
	)


func _on_clip_path_ctrlr_undo_requested(undo_times: int) -> void:
	ProjectServer2.try_exit_clip_res(undo_times)

func _on_center_btn_pressed() -> void:
	PlaybackServer.position = 0
	navigate_horizontal_to(0)
	update_timeline_view()

func _on_scroll_cont_scroll_bar_scrolling() -> void:
	update_timeline_view()

func _on_h_scrollbar_scrolling() -> void:
	center = h_scrollbar.value + displ_timemark_size_h / displ_frame_size
	update_timeline_view()

func _on_project_server_project_opened(project_res: ProjectRes) -> void:
	open_project_res(project_res)

func _on_project_server_opened_clip_res_changed(old_one: MediaClipRes, new_one: MediaClipRes) -> void:
	open_clip_res(new_one)

func _on_playback_server_position_changed(position: int) -> void:
	if PlaybackServer.is_playing():
		navigate_to_cursor(-1)
	update_timeline_view()

func _on_clip_res_layer_added(layer_idx: int, layer: LayerRes) -> void:
	spawn_layer(layer_idx, layer)
	sort_layers()

func _on_clip_res_layer_removed(layer_idx: int, layer: LayerRes) -> void:
	free_layer(layer)

func _on_clip_res_layer_moved(from_idx: int, to_idx: int, layer: LayerRes) -> void:
	sort_layers()

func _on_clip_res_clips_added(clips: Dictionary[Vector2i, MediaClipRes]) -> void:
	spawn_clips(clips)
	update_when_clips_changed()
	layers_body.emit_selected_changed()

func _on_clip_res_clips_removed(clips_coords: Array[Vector2i]) -> void:
	free_clips(clips_coords)
	update_when_clips_changed()
	layers_body.emit_selected_changed()

func _on_clip_res_clips_moved(from_coords: Array[Vector2i], to: Dictionary[Vector2i, MediaClipRes]) -> void:
	free_clips(from_coords)
	spawn_clips(to)
	sort_layers()
	update_when_clips_changed()


func _on_clip_res_clips_splited(coords: Array[Vector2i], deleted_coords: Array[Vector2i], new_clips: Dictionary[Vector2i, MediaClipRes], split_pos: int, accept_left: bool, accept_right: bool) -> void:
	
	layers_body.select_vals_by_method(
		func(port_idx: int, port_obj: Object, idx: int, metadata: Dictionary) -> bool:
			var coord: Vector2i = Vector2i(port_idx, idx)
			return coords.has(coord) or new_clips.has(coord), false
	)

func _on_clip_res_clips_updated(coords: Array[Vector2i]) -> void:
	update_clips(coords)
